module Sounds exposing (Sounds, loadingFinished, requestSounds)

import Audio exposing (AudioCmd)
import SeqDict exposing (SeqDict)


type alias Sounds =
    { collision : Audio.Source
    , charge : Audio.Source
    , charge2 : Audio.Source
    }


soundUrls : List String
soundUrls =
    [ "/blip.mp3"
    , "/charge.mp3"
    ]


requestSounds : (String -> Result Audio.LoadError Audio.Source -> msg) -> AudioCmd msg
requestSounds loadedSound =
    List.map (\url -> Audio.loadAudio (loadedSound url) url) soundUrls |> Audio.cmdBatch


loadingFinished : SeqDict String (Result Audio.LoadError Audio.Source) -> Maybe Sounds
loadingFinished sounds =
    let
        loadSound : ( List String, Maybe (Audio.Source -> b) ) -> ( List String, Maybe b )
        loadSound ( urlsLeft, soundsFinished ) =
            case ( urlsLeft, soundsFinished ) of
                ( head :: rest, Just soundsFinished_ ) ->
                    case SeqDict.get head sounds of
                        Just (Ok source) ->
                            ( rest, soundsFinished_ source |> Just )

                        _ ->
                            ( [], Nothing )

                _ ->
                    ( [], Nothing )
    in
    ( soundUrls, Just Sounds )
        |> loadSound
        |> loadSound
        |> loadSound
        |> Tuple.second
